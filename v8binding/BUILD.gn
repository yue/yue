# Copyright 2017 Cheng Zhao. All rights reserved.
# Use of this source code is governed by the license that can be found in the
# LICENSE file.

import("//v8binding/node.gni")

config("node_config") {
  defines = [
    "USING_UV_SHARED=1",
    "USING_V8_SHARED=1",
    "BUILDING_NODE_EXTENSION",
  ]

  node_dir = "//third_party/node-" + node_version

  include_dirs = [
    node_dir + "/include/node",
    node_dir + "/src",
    node_dir + "/deps",
    node_dir + "/deps/uv/include",
    node_dir + "/deps/v8/include",
  ]

  if (node_runtime == "chakracore") {
    include_dirs += [
      node_dir + "/deps/chakrashim/include",
    ]
  } else if (node_runtime == "electron") {
    defines += [
      "ELECTRON_BUILD",
    ]
  }

  if (is_mac) {
    ldflags = [
      "-undefined", "dynamic_lookup",
    ]
  } else if (is_win) {
    libs = [
      node_dir + "/" + target_cpu + "/node.lib",
    ]
  }
}

source_set("v8binding") {
  sources = [
    "arguments.cc",
    "arguments.h",
    "callback.cc",
    "callback.h",
    "callback_internal.h",
    "dict.h",
    "property.h",
    "locker.cc",
    "locker.h",
    "per_isolate_data.cc",
    "per_isolate_data.h",
    "prototype.cc",
    "prototype.h",
    "prototype_internal.h",
    "ref_method.cc",
    "ref_method.h",
    "template_util.h",
    "types.h",
    "util.h",
    "v8binding.h",
  ]

  public_configs = [ ":node_config" ]
}

