# Copyright 2016 Cheng Zhao. All rights reserved.
# Use of this source code is governed by the license that can be found in the
# LICENSE file.

import("//lua/lua.gni")
import("//testing/test.gni")

config("lua_config") {
  include_dirs = [ "//third_party/$lua_runtime-$lua_version/src" ]
  defines = [ "COMPAT53_PREFIX=compat53" ]
}

source_set("lua") {
  sources = [
    "lua.h",
    "lua_module_export.h",
    "call_context.h",
    "callback.h",
    "callback_internal.h",
    "handle.cc",
    "handle.h",
    "index.h",
    "metatable.cc",
    "metatable.h",
    "metatable_internal.h",
    "pcall.h",
    "ref_method.cc",
    "ref_method.h",
    "stack.h",
    "stack_auto_reset.h",
    "state.h",
    "table.cc",
    "table.h",
    "table_internal.h",
    "types.h",
    "user_data.h",
    "util.h",
    "//third_party/lua-compat-5.3/c-api/compat-5.3.c",
    "//third_party/lua-compat-5.3/c-api/compat-5.3.h",
  ]

  if (is_win) {
    cflags = [
      "/wd4189",  # local variable is initialized but not referenced
    ]
  }

  if (is_linux) {
    # Not sure why it is not added in base.
    libs = [ "atomic" ]
  }

  deps = [ "//base" ]
  public_configs = [ ":lua_config" ]
}

source_set("lua_unittests_source_set") {
  testonly = true

  sources = [
    "callback_unittest.cc",
    "handle_unittest.cc",
    "index_unittest.cc",
    "metatable_unittest.cc",
    "ref_method_unittest.cc",
    "stack_unittest.cc",
    "table_unittest.cc",
  ]

  public_deps = [
    ":lua",
    "//base",
    "//testing/gtest",
  ]
}

test("lua_unittests") {
  sources = [ "test/run_all_unittests.cc" ]
  deps = [
    ":lua_unittests_source_set",
    "//third_party/$lua_runtime-$lua_version:lua_src",
  ]
}

lua_module("lua_unittests_module") {
  testonly = true
  output_name = "lua_unittests"
  sources = [ "test/unittests_module.cc" ]
  deps = [
    ":lua_unittests_source_set",
    "//third_party/$lua_runtime-$lua_version:lua",
  ]
}
